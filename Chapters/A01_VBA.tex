\lstloadlanguages{VBScript}
\lstset{language=VBScript,
		basicstyle=\smaller[2],
		commentstyle=\rmfamily\smaller,
  		showstringspaces=false,
  		xleftmargin=4ex,literate={<-}{{$\leftarrow$}}1 {~}{{$\sim$}}1}
\lstset{escapeinside={(*}{*)}} 


\section{Fuel} \label{code_fuel}
\begin{lstlisting}
Option Explicit
Public plane, C5, C17, C130, fromBase, toBase As Long
Public selectMDS As String

Function fuelCalculations(selectMDS, distance, Payload, numStops)
'Find the cost of fuel for the max payload
    Dim specRange, wgross, gwForMaxDist, fuel, wff, wfc, wfd, A, B, C, D, _
    fuelReq, myAnswer As Double
    Dim i, j, num As Long
    Dim plane, wop, wgmt, wmaxfuel, wpmax, wfrah, wfstto, wfapp, wp, alt As Double
    Dim distRange, climb, cruise, descent, given As Range
    
    Set distRange = Range("rangeBeta")
    Set cruise = Range("cruiseBeta")
    Set climb = Range("climbBeta")
    Set descent = Range("descentBeta")
    Set given = Range("GivensTable")
    
    plane = getPlane(selectMDS)
    
    'for ease of reading the equations.
    wop = given(plane, 2)
    wgmt = given(plane, 3)
    wpmax = given(plane, 5)
    wfrah = given(plane, 6)
    wfstto = given(plane, 7)
    wfapp = given(plane, 8)
    alt = given(plane, 9)
    
    wmaxfuel = wgmt - wop - wfrah - Payload
    If wmaxfuel + wfrah > given(plane, 4) Then wmaxfuel = given(plane, 4) - wfrah
    wgross = wop + Payload + wmaxfuel + wfrah 'gross takeoff weight
    num = numStops + 1

    'A-D are used in the fuelConsumed Function, found under Equation 23 from
    'Reiman 2014 page 52
    A = cruise(plane, 6) / 3
    B = cruise(plane, 5) / 2 + cruise(plane, 6) * (wop + wfrah + Payload) + _
    (cruise(plane, 7) / 2) * alt
    C = cruise(plane, 2)+cruise(plane, 3)*alt+cruise(plane, 4)*(alt)^2 + _
        cruise(plane, 5) * (wop + wfrah + Payload) + _
        cruise(plane, 6) * (wop + wfrah + Payload)^ 2 + _
        cruise(plane, 7) * alt * (wop + wfrah + Payload)
    D = -distance / num
    
    wff = fuelConsumed(A, B, C, D)
    
    'Equation 19 and 20 from Reiman 2014 page 50
    wfc = climb(plane, 2) + climb(plane, 3) * alt + climb(plane, 4) * (alt)^ 2 + _
          climb(plane, 5) * (alt) ^ 3 + climb(plane, 6) * wgross + _
          climb(plane, 7) * (wgross) ^ 2 + climb(plane, 8) * (wgross) ^ 3 + _
          climb(plane, 9) * 10 ^ (-6) * (alt) ^ 2 * (wgross)^ 3 + _
          climb(plane, 10) * 10 ^ (-6) * (alt) ^ 2 * (wgross)^ 3
    wfd = descent(plane, 2)+descent(plane, 3)*(wgross-wfc-wff-wfstto) + _
          descent(plane, 4)*(wgross-wfc-wff-wfstto)^ 2 + descent(plane, 5)*alt + _
          descent(plane, 6) * alt * (wgross - wfc - wff - wfstto)
    
    fuel = num * (wff + wfc + wfd)
     
    fuelCalculations = fuel
End Function

'this function returns the Klbs fuel consumed during the cruise portion of flight
Function fuelConsumed(A, B, C, D) As Double
    Dim commonTerm1, commonTerm2, cubeRoot1, cubeRoot2 As Double
    
    commonTerm1 = 2# * (B ^ 3) - 9# * A * B * C + 27# * (A^ 2) * D
    commonTerm2 = 4 * ((B ^ 2) - 3 * A * C)^ 3
    cubeRoot1 = 0.5 * (commonTerm1 + Sqr((commonTerm1^ 2) - commonTerm2))
    cubeRoot2 = 0.5 * (commonTerm1 - Sqr((commonTerm1^ 2) - commonTerm2))
    
        If (cubeRoot1 < 0) Then
            cubeRoot1 = -(-cubeRoot1)^ (1 / 3)
        Else
            cubeRoot1 = (cubeRoot1^ (1 / 3))
        End If
    
        If (cubeRoot2 < 0) Then
            cubeRoot2 = -(-cubeRoot2)^ (1 / 3)
        Else
            cubeRoot2 = (cubeRoot2^ (1 / 3))
            End If
    
        '//g=-B/3A
        '//  -1/3A(1/2 [2B^3-9ABC+27A^2 D+v((2B^3-9ABC+27A^2 D)^2-4(B^2-3AC)^3)])
        '//  -1/3A(1/2 [2B^3-9ABC+27A^2 D-v((2B^3-9ABC+27A^2 D)^2-4(B^2-3AC)^3)])
        
        fuelConsumed = (-B/(3*A)) - (1/(3*A)) * cubeRoot1 - (1/(3*A)) * cubeRoot2

End Function

'This function facilitates the fuel costs (accounts for multiple enroute stops) 
Function fuel(selectMDS, distance, Payload)
    Dim numberStops As Long
    numberStops = numStops(selectMDS, distance, Payload)
    fuel = fuelCalculations(selectMDS, distance, Payload, numberStops)
End Function
\end{lstlisting}
\Section{Distance}
\begin{lstlisting}
'Calculates the vincenty elliptical great circle geodetic curve 
'(Code courtesy of Mike Gavaghan)
Function vincentyDistance(lat1, long1, lat2, long2) As Double
    'Ellipsoid properties based on WGS-84
    Dim semiMajor, inverseFlattening, flattening, semiMinor, A, B, big_A, big_B, _
    f, TwoPi, phi1, phi2, lambda, lambda1, lambda2, a2b2b2, tanphi1, tanU1, U1, _
    sinU1 As Double
    Dim cosU1, tanphi2, tanU2, U2, small_u2, sinU2, cosU2, sinU1sinU2, _
    cosU1sinU2, sinU1cosU2, cosU1cosU2, a2, b2, omega, i, s As Double
    Dim sigma, deltasigma, lambda0, sinlambda, coslambda, sin2sigma, sinsigma, _
    cossigma, sinalpha, alpha, cosalpha, cos2alpha, cos2sigmam, cossigmam, _
    cos2sigmam2, C  As Double
    Dim converged As Boolean
    Dim change As Variant
        
        semiMajor = 6378137#   'Meters
        inverseFlattening = 298.257223563
        flattening = 1# / inverseFlattening
        semiMinor = (1# - flattening) * semiMajor
        
        Application.ScreenUpdating = False

    'simplify
        A = semiMajor
        B = semiMinor
        f = flattening
        TwoPi = 2# * Application.Pi

    'get parameters as radians
        phi1 = lat1 * Application.Pi / 180#
        lambda1 = long1 * Application.Pi / 180#
        phi2 = lat2 * Application.Pi / 180#
        lambda2 = long2 * Application.Pi / 180#

    'calculations
        a2 = A * A
        b2 = B * B
        a2b2b2 = (a2 - b2) / b2

        omega = lambda2 - lambda1

        tanphi1 = Tan(phi1)
        tanU1 = (1# - f) * tanphi1
        U1 = Atn(tanU1)
        sinU1 = Sin(U1)
        cosU1 = Cos(U1)
    
        tanphi2 = Tan(phi2)
        tanU2 = (1# - f) * tanphi2
        U2 = Atn(tanU2)
        sinU2 = Sin(U2)
        cosU2 = Cos(U2)
    
        sinU1sinU2 = sinU1 * sinU2
        cosU1sinU2 = cosU1 * sinU2
        sinU1cosU2 = sinU1 * cosU2
        cosU1cosU2 = cosU1 * cosU2

    'eq. 13
        lambda = omega

    'intermediates we'll need to compute 's'
    
        big_A = 0#
        big_B = 0#
        sigma = 0#
        deltasigma = 0#
        converged = False

    For i = 0 To 20
        lambda0 = lambda

        sinlambda = Sin(lambda)
        coslambda = Cos(lambda)

        'eq. 14
        sin2sigma = (cosU2 * sinlambda * cosU2 * sinlambda) + _
        (cosU1sinU2 - sinU1cosU2 * coslambda) ^ 2
        sinsigma = Sqr(sin2sigma)

        'eq. 15
        cossigma = sinU1sinU2 + (cosU1cosU2 * coslambda)

        'eq. 16
        sigma = Application.Atan2(cossigma, sinsigma)

        'eq. 17    Careful!  sin2sigma might be almost 0!
         If sin2sigma = 0 Then sinalpha = 0 Else sinalpha = _
                                                cosU1cosU2 * sinlambda / sinsigma
        Dim check As Variant
        check = sigma * 2
        alpha = Application.Asin(sinalpha)
        cosalpha = Cos(alpha)
        cos2alpha = cosalpha * cosalpha

        'eq. 18    Careful!  cos2alpha might be almost 0!
        If cos2alpha = 0# Then cos2sigmam = 0# Else cos2sigmam = _
                                            cossigma - 2 * sinU1sinU2 / cos2alpha
        small_u2 = cos2alpha * a2b2b2

        cos2sigmam2 = cos2sigmam * cos2sigmam

        'eq. 3
        big_A = 1+small_u2/16384*(4096+small_u2*(-768+small_u2*(320-175*small_u2)))

        'eq. 4
        big_B = small_u2/1024*(256+small_u2*(-128+small_u2*(74-47*small_u2)))

        'eq. 6
        deltasigma = big_B*sinsigma*(cos2sigmam+big_B / 4*(cossigma * _
        (-1+2*cos2sigmam2)-big_B/6*cos2sigmam*(-3+4*sin2sigma)*(-3+4*cos2sigmam2)))

        'eq. 10
        C = f / 16 * cos2alpha * (4 + f * (4 - 3 * cos2alpha))

        'eq. 11 (modified)
        lambda = omega+(1-C)*f*sinalpha*(sigma+C*sinsigma*(cos2sigmam+C*cossigma* _
                                                                (-1+2*cos2sigmam2)))

        'see how much improvement we got
        change = Math.Abs((lambda - lambda0) / lambda)

        If i > 1 And change < 0.0000000000001 Then
            converged = True
            Exit For
        End If
    Next
    
    'eq. 19
    s = B * big_A * (sigma - deltasigma)
    
    vincentyDistance = s / 1852

End Function

' Given the altitude in 1,000s of feet, Klbs reserve, alternate and holding (rah) fuel,
'Klbs payload, Klbs fuel consumed (max fuel after payload) in Klbs,
'determines the max distance possible
Function maxDistance(selectMDS, Payload) As Double
   Dim wop, wgmt, wmaxfuel, wpmax, wfrah, wfstto, wfapp, alt, A, B, C, G, _
   totalFuelConsumed As Double
   Dim cruise, climb, descent, given As Range
    
    Set cruise = Range("cruiseBeta")
    Set climb = Range("climbBeta")
    Set descent = Range("descentBeta")
    Set given = Range("GivensTable")
    
    maxDistance = 0
    plane = getPlane(selectMDS)
    
    'for ease of reading the equations.
    wop = given(plane, 2)
    wgmt = given(plane, 3)
    wmaxfuel = given(plane, 4)
    wpmax = given(plane, 5)
    wfrah = given(plane, 6)
    wfstto = given(plane, 7)
    wfapp = given(plane, 8)
    alt = given(plane, 9)
    
   'difference from max gross takeoff and payload and reserve
    totalFuelConsumed = wgmt - wop - Payload - wfrah 
    
    If totalFuelConsumed+wfrah > wmaxfuel Then totalFuelConsumed = wmaxfuel-wfrah
    
'Use Formula distance = A*g^3 + B*g^2 + C*g coefficients. G represents fuel consumed.
    A = cruise(plane, 6)/3
    B = cruise(plane, 5)/2+cruise(plane, 6)*(wop+Payload+wfstto+wfapp+wfrah)+ _
    cruise(plane, 7) * (alt) / 2# '(ß_3/2+ß_4*(EW+w)+ß_5/2*Alt)
    C = cruise(plane, 2) + cruise(plane, 3) * alt + cruise(plane, 4) * (alt) ^ 2 + _
            cruise(plane, 5) * (wop + Payload + wfstto + wfapp + wfrah) + _
            cruise(plane, 6) * (wop + Payload + wfstto + wfapp + wfrah) ^ 2 + _
            cruise(plane, 7) * alt * (wop + Payload + wfstto + wfapp + wfrah)
    '(ß_0+ß_1*Alt+ß_2*A?lt?^2+ß_3*(EW+w)+ß_4*(EW+w)^2+ß_5*Alt*(EW+w))
    G = totalFuelConsumed

    maxDistance = A * ((G) ^ 3) + B * ((G) ^ 2) + C * G
End Function

'calculates the number of stops required for an MDS given the distance and payload
Function numStops(selectMDS, distance, Payload) As Double
    Dim stopDist As Double

    stopDist = maxDistance(selectMDS, Payload)
    numStops = distance / stopDist
    numStops = Application.WorksheetFunction.RoundDown(numStops, 0)
End Function

'given source and destination ICAOs this function finds the associated lats and longs
'and returns the distance between them
Function myDistance(fromString, toString) As Double

    Dim rowCount, colCount, aircraftCount, totalFromBases, totalToBases, totalNumAircraft, _
    i, k, col As Long
    Dim tempstring, fromPCN, toPCN As String
    Dim lat1, lat2, long1, long2, Payload, distance, baseFuel, maxpayload As Double
    Dim endCond As Boolean
    
    'Find lat/long data for "from base
    For i = 1 To 5342
        If Sheets("AirfieldData").Cells(i, 2) = fromString Then
            lat1 = Sheets("AirfieldData").Cells(i, 3)
            long1 = Sheets("AirfieldData").Cells(i, 4)
            'fromPCN = Sheets("AirfieldData").Cells(i, 7)
            Exit For
        End If
    Next
        
    'Find the lat/long for the "to" base
    For i = 1 To 5342
        If Sheets("AirfieldData").Cells(i, 2) = toString Then
            lat2 = Sheets("AirfieldData").Cells(i, 3)
            long2 = Sheets("AirfieldData").Cells(i, 4)
            'toPCN = Sheets("AirfieldData").Cells(i, 7)
            Exit For
        End If
    Next i
    
    'Populate distance
    myDistance = vincentyDistance(lat1, long1, lat2, long2)
                     
End Function
\end{lstlisting}
\Section{Pre-Processing} \label{code_preprocessing}
\begin{lstlisting}
'this sub moves through all the source and destination combinations and populates 
'the Aircraft sheet with the distance between them, fuel cost per payload pound, 
'and basic fuel cost given the aircraft MDS
Sub getDistance()
    Dim rowCount, colCount, aircraftCount, totalFromBases, totalToBases, _
    totalNumAircraft, i, k, col As Long
    Dim fromString, toString, tempstring, fromPCN, toPCN As String
    Dim lat1, lat2, long1, long2, Payload, distance, baseFuel, maxpayload, _
    payloadFuel As Double
    Dim endCond As Boolean
    
    Application.ScreenUpdating = False
    rowCount = 0
    colCount = 0
    aircraftCount = 0
    Payload = 0
    
    Do While endCond = False
        tempstring = Sheets("SupplyBases").Cells(rowCount + 2, 1)
        If IsEmpty(tempstring) Then
            endCond = True
            Exit Do
        End If
        rowCount = rowCount + 1
    Loop
    
    totalFromBases = rowCount
    endCond = False
    Do While endCond = False
        tempstring = Sheets("DemandBases").Cells(colCount + 2, 1)
       If IsEmpty(tempstring) Or tempstring = "" Then
            endCond = True
            Exit Do
        End If
        colCount = colCount + 1
    Loop
    
    totalToBases = colCount
    
    endCond = False
    Do While endCond = False
        tempstring = Sheets("Aircraft").Cells(aircraftCount + 8, 1)
       If IsEmpty(tempstring) Or tempstring = "" Then
            endCond = True
            Exit Do
        End If
        aircraftCount = aircraftCount + 1
    Loop
        
    totalNumAircraft = aircraftCount
    col = 1
     
     Application.ScreenUpdating = False
    For rowCount = 1 To totalFromBases
        fromString = Sheets("SupplyBases").Cells(rowCount + 1, 1)
        'Find lat/long data for "from base
        For i = 1 To 5342
            If Sheets("AirfieldData").Cells(i, 2) = fromString Then
                lat1 = Sheets("AirfieldData").Cells(i, 3)
                long1 = Sheets("AirfieldData").Cells(i, 4)
                fromPCN = Sheets("AirfieldData").Cells(i, 7)
                Exit For
            End If
        Next
        
        'Find the lat/long for the "to" base
        For colCount = 1 To totalToBases
            toString = Sheets("DemandBases").Cells(colCount + 1, 1)
            If Not toString = fromString Then
                For i = 1 To 5342
                    If Sheets("AirfieldData").Cells(i, 2) = toString Then
                        lat2 = Sheets("AirfieldData").Cells(i, 3)
                        long2 = Sheets("AirfieldData").Cells(i, 4)
                        toPCN = Sheets("AirfieldData").Cells(i, 7)
                        Exit For
                    End If
                    Sheets("Aircraft").Cells(6, 14 + col) = fromString
                    Sheets("Aircraft").Cells(7, 14 + col) = toString
                    Sheets("maxfuel").Cells(1, 1 + col) = fromString
                    Sheets("maxfuel").Cells(2, 1 + col) = toString
                    Sheets("basefuel").Cells(1, 1 + col) = fromString
                    Sheets("basefuel").Cells(2, 1 + col) = toString
                
                Next i
                
                'Populate distance and fuel cost/(payload Klb) given aircraft type for all 
                'aircraft on "aircraft" sheet
                distance = vincentyDistance(lat1, long1, lat2, long2)
                For k = 1 To totalNumAircraft
                    selectMDS = Sheets("Aircraft").Cells(k + 7, 2).Value
                    Payload = Sheets("Aircraft").Cells(k + 7, 6).Value
                    maxpayload = Sheets("Aircraft").Cells(k + 7, 6).Value
                    
                    Sheets("Aircraft").Cells(k+7, 14).Value = _
                                                        Sheets("Aircraft").Cells(k+7, 1)
                    Sheets("maxfuel").Cells(k+2, 1).Value = _
                                                        Sheets("Aircraft").Cells(k+7, 1)
                    Sheets("basefuel").Cells(k+2, 1).Value = _ 
                                                        Sheets("Aircraft").Cells(k+7, 1)
                    
                     Sheets("Aircraft").Cells(k + 7, 13 + col + 1).Value = distance
                     'Sheets("Aircraft").Cells(k + 3, 14 + col).Value = _
                     airfieldNodalReduction(selectMDS, lat1, long1, lat2, long2, _
                     fromPCN, toPCN, payload)
                     baseFuel = fuel(selectMDS, distance, 0)
                     payloadFuel = fuel(selectMDS, distance, Payload)
                     
                     Sheets("maxfuel").Cells(k + 2, 1 + col).Value = _
                     (payloadFuel - baseFuel) / maxpayload
                     
                     Sheets("basefuel").Cells(k + 2, 1 + col).Value = baseFuel
                Next k
                col = col + 1
            End If
        Next colCount
        
    Next rowCount
Application.ScreenUpdating = True
End Sub

\end{lstlisting}

\section{Post Processing}
\begin{lstlisting}
Sub FinalCost()
    Dim endCond As Boolean
    Dim tempstring, toString, fromString, planeString As String
    Dim aircraftCount, allocatedCount, baseCount, i, j, k As Long
    Dim distance, Payload As Double
      
     aircraftCount = 0
     allocatedCount = 0
     baseCount = 0
     
     Application.ScreenUpdating = False
    
    'count the number of aircraft
    endCond = False
    Do While endCond = False
        tempstring = Sheets("Aircraft").Cells(aircraftCount + 8, 1)
       If IsEmpty(tempstring) Or tempstring = "" Then
            endCond = True
            Exit Do
        End If
        aircraftCount = aircraftCount + 1
    Loop
    
    'count the number of allocated aircraft
    endCond = False
    Do While endCond = False
        tempstring = Sheets("ACAllocation").Cells(1, allocatedCount + 3)
       If IsEmpty(tempstring) Or tempstring = "" Then
            endCond = True
            Exit Do
        End If
        allocatedCount = allocatedCount + 1
    Loop
    
   'count the number of bases
    endCond = False
    Do While endCond = False
        tempstring = Sheets("ACAllocation").Cells(baseCount + 2, 1)
       If IsEmpty(tempstring) Or tempstring = "" Then
            endCond = True
            Exit Do
        End If
        baseCount = baseCount + 1
    Loop
    
    For i = 1 + 1 To baseCount + 1 'go down the rows of bases
        fromString = Sheets("ACAllocation").Cells(i, 1)
        toString = Sheets("ACAllocation").Cells(i, 2)
        For j = 1 + 2 To allocatedCount + 3 'go across the columns of allocated
            planeString = Sheets("ACAllocation").Cells(1, j)
            For k = 1 To aircraftCount 'loop through the total aircraft
                tempstring = Sheets("Aircraft").Cells(k + 7, 1)
                If planeString = tempstring And _ 
                                 Sheets("ACAllocation").Cells(i, j) > 0 Then
                            selectMDS = Sheets("Aircraft").Cells(k + 7, 2).Value
                    distance = myDistance(fromString, toString)
                    Payload = Sheets("FinalPayload").Cells(i, j).Value
                    Sheets("finalFuel").Cells(1, j) = planeString
                    Sheets("finalFuel").Cells(i, 1) = fromString
                    Sheets("finalFuel").Cells(i, 2) = toString
                    'cycle cost is payload to destination, empty back
                    Sheets("finalFuel").Cells(i, j) = _
                        fuel(selectMDS, distance, Payload)+fuel(selectMDS, distance, 0)
                    Exit For
                End If
            Next k
        Next j
    Next i
    Application.ScreenUpdating = True
End Sub
\end{lstlisting}